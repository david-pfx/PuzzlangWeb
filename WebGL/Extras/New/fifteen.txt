title Fifteen
author puzzlang
homepage puzzlang.org

background_color lightbrown
text_color darkblue
run_rules_on_level_start
hover
noarrows

========
OBJECTS
========
Background .
lightblue

edge *
lightbrown

info +
lightbrown

player
transparent

N
red

W
lightbrown

X
lightbrown

Y
lightbrown

Z
lightbrown

lit
transparent

focus (for hover)
lightgrey
000000000000
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
000000000000

grid
darkgrey
000000000000
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
000000000000

0 0.7
transparent

1 0.8
blue
message 1

2 0.8
blue
message 2

3 0.7
blue
message 3

4 0.7
blue
message 4

5 0.7
blue
message 5

6 0.7
blue
message 6

7 0.7
blue
message 7

8 0.7
blue
message 8

9 0.7
blue
message 9

A 0.7
blue
message 10

B 0.7
blue
message 11

C 0.7
blue
message 12

D 0.7
blue
message 13

E 0.7
blue
message 14

F 0.7
blue
message 15

tyou 0.5
blue
message You

twin 0.5
blue
message Win!

=======
LEGEND
=======

digit = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or A or B or C or D or E or F
clickable = 0 or digit
mark = W or X or Y or Z
P = player and edge

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
edge W X Y Z N grid info 
lit
0 digit player tyou twin
focus

======
RULES
======

[ action player ] -> reset
[ moving player ] -> [ player ]

[ tyou ] -> cancel

[ W | N | N | N | N ] -> [ W | N 1 | N 2 | N 3 | N 4 ]
[ X | N | N | N | N ] -> [ X | N 5 | N 6 | N 7 | N 8 ]
[ Y | N | N | N | N ] -> [ Y | N 9 | N A | N B | N C ]
[ Z | N | N | N | N ] -> [ Z | N D | N E | N F |   0 ]

(random [ N digit | 0 ] -> [ 0 | digit ] )

startloop
random [ N digit | 0 ] -> [ 0 | digit ] 
random [ N digit | | | 0 ] -> [ 0 | | | digit ] 
random [ N ] [ no N digit | 0 ] -> [ N ] [ 0 | digit ]
random [ N ] [ no N digit | | | 0 ] -> [ N ] [ 0 | | | digit ]
endloop

[ N ] -> [ ]

[ focus ] -> [ ]
[ hover clickable ] -> [ focus clickable ]

[ fire1 digit | 0 ] -> [ 0 | digit ]

[ grid ] -> [ ]
[ digit ] -> [ digit grid ]

[ lit ] -> [ ]
right [ W | 1 | 2 | 3 | 4 ] -> [ W lit | 1 | 2 | 3 | 4 ]
right [ X | 5 | 6 | 7 | 8 ] -> [ X lit | 5 | 6 | 7 | 8 ]
right [ Y | 9 | A | B | C ] -> [ Y lit | 9 | A | B | C ]
right [ Z | D | E | F | 0 ] -> [ Z lit | D | E | F | 0 ]

([ lit | lit | lit | lit ] [ info ] -> message You win!)
down [ lit | lit | lit | lit ] [ info ] -> [ lit | lit | lit | lit ] [ info tyou ]
right [ info tyou | info tyou ] -> [ info tyou | info twin ]

=======
LEVELS
=======

P*****
WNNNN*
XNNNN*
YNNNN*
ZNNNN*
**++**