title Bridges
author puzzlang
homepage puzzlang.org

color_palette mastersystem

background_color blue
text_color yellow

noarrows
noaction

========
OBJECTS
========
Background .
green

clickable -
transparent

edge *
blue green lightblue lightgreen
0123
3012
2301
1230

lit
yellow

cell
darkblue
.000000.
0......0
0......0
0......0
0......0
0......0
0......0
.000000.

( numbers in cells -- macro would help)
N1 0.7
black
message 1

N2 0.7
black
message 2

N3 0.7
black
message 3

N4 0.7
black
message 4

N5 0.7
black
message 5

N6 0.7
black
message 6

N7 0.7
black
message 7

N8 0.7
black
message 8

singley
darkblue
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....

singlex
darkblue
.........
.........
.........
.........
000000000
.........
.........
.........
.........

doubley
darkblue
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...

doublex
darkblue
.........
.........
.........
000000000
.........
000000000
.........
.........
.........

c0
transparent
message A

c1
transparent
message B

c2
transparent
message C

c3
transparent
message D

c4
transparent
message E 

c5
transparent
message F 

c6
transparent
message G

c7
transparent
message H 

c8
transparent
message I

=======
LEGEND
=======

number = n1 or n2 or n3 or n4 or n5 or n6 or n7 or n8
1 = n1 and cell
2 = n2 and cell
3 = n3 and cell
4 = n4 and cell
5 = n5 and cell
6 = n6 and cell
7 = n7 and cell
8 = n8 and cell
single = singley or singlex
double = doubley or doublex
linex = singlex or doublex
liney = singley or doubley
line = single or double
count  = c0 or c1 or c2 or c3 or c4 or c5 or c6 or c7 or c8
count1 = c1 or c2 or c3 or c4 or c5 or c6 or c7 or c8 or c8
count2 = c2 or c3 or c4 or c5 or c6 or c7 or c8 or c8 or c8

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
edge 
count lit
clickable
line
cell
number

======
RULES
======

( single line )
horizontal [ cell    | fire1 clickable no line ]            -> [ cell    | clickable action singlex ]
horizontal [ cell    | action singlex | clickable no line ] -> [ cell    | singlex | clickable action singlex ]
horizontal [ singlex | action singlex | clickable no line ] -> [ singlex | singlex | clickable action singlex ]

horizontal [ cell    | fire1 clickable singlex ]            -> [ cell    | clickable action doublex ]
horizontal [ cell    | action doublex | singlex ]           -> [ cell    | doublex | action doublex ]
horizontal [ doublex | action doublex | singlex ]           -> [ doublex | doublex | action doublex ]

( promote single to double )
vertical   [ cell    | fire2 clickable no line ]            -> [ cell    | clickable action singley ]
vertical   [ cell    | action singley | clickable no line ] -> [ cell    | singley | clickable action singley ]
vertical   [ singley | action singley | clickable no line ] -> [ singley | singley | clickable action singley ]

vertical   [ cell    | fire2 clickable singley ]            -> [ cell    | clickable action doubley ]
vertical   [ cell    | action doubley | singley ]           -> [ cell    | doubley | action doubley ]
vertical   [ doubley | action doubley | singley ]           -> [ doubley | doubley | action doubley ]

( remove line )
horizontal [ cell | fire1 clickable doublex ] -> [ cell | clickable ]
vertical   [ cell | fire2 clickable doubley ] -> [ cell | clickable ]

( remove loose ends )
horizontal [ linex | clickable no line ] -> [ | clickable ]
vertical   [ liney | clickable no line ] -> [ | clickable ]

[ cell ] -> [ cell c0 ]
up    [ cell count | singley ] -> [ cell count1 | singley ]
down  [ cell count | singley ] -> [ cell count1 | singley ]
left  [ cell count | singlex ] -> [ cell count1 | singlex ]
right [ cell count | singlex ] -> [ cell count1 | singlex ]
up    [ cell count | doubley ] -> [ cell count2 | doubley ]
down  [ cell count | doubley ] -> [ cell count2 | doubley ]
left  [ cell count | doublex ] -> [ cell count2 | doublex ]
right [ cell count | doublex ] -> [ cell count2 | doublex ]

[ lit ] -> [ ]
[ 1 c1 ] -> [ 1 lit ]
[ 2 c2 ] -> [ 2 lit ]
[ 3 c3 ] -> [ 3 lit ]
[ 4 c4 ] -> [ 4 lit ]
[ 5 c5 ] -> [ 5 lit ]
[ 6 c6 ] -> [ 6 lit ]
[ 7 c7 ] -> [ 7 lit ]
[ 8 c8 ] -> [ 8 lit ]

==============
WINCONDITIONS
==============

 all cell on lit

=======
LEVELS
=======

message Left click to draw horizontal line, right click vertical

message The aim is to light up all the boxes

message Small 7x7 puzzle

***********
*.........*
*.3-----4.*
*.-------.*
*.3-5-2-4.*
*.-------.*
*.3-6---5.*
*.-------.*
*.4-3-2-4.*
*.........*
***********

message Medium 10x10 puzzle

**************
*............*
*.-2--3-2-4-.*
*.3--1-----2.*
*.--2-5---4-.*
*.5--4-2----.*
*.----------.*
*.-1-3-4--3-.*
*.4---6----5.*
*.----------.*
*.2---4---1-.*
*.---2-3---3.*
*............*
**************

message Large 15x15 puzzle

*******************
*.................*
*.2-5-----------3.*
*.---1-4-5---5-2-.*
*.2---1----2-----.*
*.-------------2-.*
*.2----2---------.*
*.----4--4-4-----.*
*.--4---4----5-4-.*
*.-------1-2-----.*
*.----------1----.*
*.--4---6--3-----.*
*.-------------3-.*
*.---------------.*
*.--2---4-2----2-.*
*.----3-----2----.*
*.2--3-------4--3.*
*.................*
*******************
